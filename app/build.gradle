apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.simplemobiletools.musicplayer"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 111
        versionName "5.16.5"
        setProperty("archivesBaseName", "music-player")
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "variants"
    productFlavors {
        core {}
        fdroid {}
        prepaid {}
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }
}

dependencies {
    implementation 'com.github.SimpleMobileTools:Simple-Commons:bce3441c67'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.airbnb.android:lottie:3.6.1'
    implementation 'com.github.bjoernpetersen:m3u-parser:1.3.0'
    implementation 'me.grantland:autofittextview:0.2.1'
    coreLibraryDesugaring('com.android.tools:desugar_jdk_libs:1.1.5')

    // higher versions of jaudiotagger not compatible with <= API 25 devices
    // https://bitbucket.org/ijabz/jaudiotagger/issues/149/some-nio-classes-are-unavailable-while
    implementation "net.jthink:jaudiotagger:2.2.5"
    kapt "androidx.room:room-compiler:2.5.1"
    implementation "androidx.room:room-runtime:2.5.1"
    annotationProcessor "androidx.room:room-compiler:2.5.1"
}
